// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = "1.3.72"
        ktlint_version = '8.2.0'

        arch_version = '2.1.0'
        arch_lifecycle_version = '2.2.0'
        arch_livecycle_livedata_version = '2.2.0'
        arch_lifecycle_ktx = '2.2.0'
        arch_room_version = '2.2.3'
        arch_room_ktx = '2.2.3'
        arch_livedata_ktx = '2.2.0'
        arch_workmanager_version = '2.3.0'
        arch_paging_runtime_version = '2.1.1'

        androidx_version = '1.1.0'
        androidx_cardview_version = '1.0.0'
        androidx_browser_version = '1.2.0'
        androidx_core_ktx_version = '1.1.0'
        androidx_appcompat_version = '1.1.0'
        androidx_multidex_version = '2.0.1'

        coroutines_version = '1.3.8'

        design_support_version = '1.2.0-alpha04'

        constraint_layout_version = '1.1.3'

        dagger_version = '2.27'

        timber_version = '4.7.1'

        retrofit2 = "2.8.1"
        okhttp3 = "4.7.0"
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.3' // google-services plugin
        classpath 'com.android.tools.build.jetifier:jetifier-processor:1.0.0-beta09'
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.google.firebase:perf-plugin:1.3.1'
        classpath 'io.fabric.tools:gradle:1.31.2'
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.0.0"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.2.0'
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.0.1"
    id "org.jlleitschuh.gradle.ktlint" version "$ktlint_version"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.google.com' }
        // This is for library : com.facetec:zoom-authentication
        // Reference local .aar file if it doesn't exist in maven
        flatDir { dirs '../../' }
        maven { url 'https://jitpack.io' }
        maven {
            name 'glide-snapshot'
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "1000"
        }
    }
}

spotless {
    // Optionally configure plugin
    ktlint {
        debug.set(true)
    }
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat().aosp()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    kotlin {
        target "**/*.kt"
        ktlint(ktlint_version)
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'xml', {
        target '**/*.xml'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

ext {
    KEYSTORE_STORE_FILE = rootProject.file(keystoreProperties['SIGNED_STORE_FILE'])
    KEYSTORE_STORE_PASSWORD = keystoreProperties['SIGNED_STORE_PASSWORD']
    KEYSTORE_KEY_ALIAS = keystoreProperties['SIGNED_KEY_ALIAS']
    KEYSTORE_KEY_PASSWORD = keystoreProperties['SIGNED_KEY_PASSWORD']
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }

        if (requested.group == "androidx.appcompat") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "1.+"
            }
        }
    }

    exclude group: 'com.google.protobuf', module: 'protobuf-lite'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'

    exclude group: "com.android.support", module: "support-core-ui"
    exclude group: "com.android.support", module: "support-compat"
}
